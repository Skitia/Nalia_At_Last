//Appends to Nalia25.bcs

//Set Nalia Romance ToB active if either SoA Romance is active.
IF
InParty(Myself)
GlobalLT("NaliaRomanceActive","GLOBAL",2)
GlobalLT("PGNaliaRomanceActive","GLOBAL",2)
!Global("JaheiraRomanceActive","GLOBAL",2)
!Global("AerieRomanceActive","GLOBAL",2)
!Global("ViconiaRomanceActive","GLOBAL",2)
!Global("NeeraRomanceActive","GLOBAL",2)
!Global("C0SireneRomanceActive","GLOBAL",2)
!Global("L3PetsyRomanceActive","GLOBAL",2)
!Global("YvetteRomanceActive","GLOBAL",2)
!Global("X3EmiRomanceActive","GLOBAL",2)
!Global("X3RebRomanceActive","GLOBAL",2)
!Global("X3KalRomanceActive","GLOBAL",2)
!Global("X3VieRomanceActive","GLOBAL",2)
Gender(Player1,MALE)
Global("X3NaliaMatch","GLOBAL",0)
THEN RESPONSE #100
SetGlobal("X3NaliaMatch","GLOBAL",1) //Our match does not check for race, or alignment.
END 

//Friendship Talk Initialization 
IF 
InParty(Myself)
!Global("PGNaliaRomanceActive","GLOBAL",1)
!Global("PGNaliaRomanceActive","GLOBAL",2)
!Global("NaliaRomanceActive","GLOBAL",2)
Global("X3NaliaFriendship","GLOBAL",0)
THEN RESPONSE #100 
RealSetGlobalTimer("X3NaliaToBLoveTimer","GLOBAL",21)
SetGlobal("X3NaliaFriendship","GLOBAL",1)
END 
//Standalone Love Talk Initialization
IF 
InParty(Myself)
Global("PGNaliaRomanceActive","GLOBAL",1)
Global("X3NaliaLove","GLOBAL",0)
THEN RESPONSE #100 
RealSetGlobalTimer("X3NaliaToBLoveTimer","GLOBAL",19)
SetGlobal("X3NaliaLove","GLOBAL",1)
END

//Engagement Ring Crossmod
IF 
HasItemEquiped("X3ERing",Myself)
Global("X3Engagement","LOCALS",0)
IsValidForPartyDialogue(Myself)
IsValidForPartyDialogue(Player1)
CombatCounter(0)
!See([ENEMY])
THEN RESPONSE #100 
IncrementGlobal("X3Engagement","LOCALS",1)
StartDialogueNoSet(Player1)
END  

IF 
HasItemEquiped("X3ERing",Myself)
Global("X3Engagement","LOCALS",2)
IsValidForPartyDialogue(Myself)
IsValidForPartyDialogue(Player1)
CombatCounter(0)
!See([ENEMY])
THEN RESPONSE #100 
DisplayString("NALIA",@3)
ActionOverride(Player1,TakePartyItem("X3ERing"))
END 

// Flirts
IF
InParty(Myself)
Global("X3NaliaFlirtsStart","LOCALS",0)
OR(3)
Global("PGNaliaRomanceActive","GLOBAL",2)
Global("PGNaliaRomanceActive","GLOBAL",1)
Global("NaliaRomanceActive","GLOBAL",2)
OR(2)
GlobalGT("X3NaliaLove","GLOBAL",5)
GlobalGT("PGNaliaLove","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("X3NaliaFlirtsStart","LOCALS",1)
RealSetGlobalTimer("X3NaliaFlirtTimer","GLOBAL",300)
END

IF
InParty(Myself)
RealGlobalTimerExpired("X3NaliaFlirtTimer","GLOBAL")
Global("X3NaliaStartFlirt","GLOBAL",0)
Global("X3DisableFlirts","LOCALS",0)
Global("X3NaliaSilence","GLOBAL",0)
!StateCheck(Myself,CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
OR(3)
Global("PGNaliaRomanceActive","GLOBAL",2)
Global("PGNaliaRomanceActive","GLOBAL",1)
Global("NaliaRomanceActive","GLOBAL",2)
See(Player1)
CombatCounter(0)
!See([ENEMY])
THEN
RESPONSE #100
SetGlobal("X3NaliaStartFlirt","GLOBAL",1)
END

IF
InParty(Myself)
Global("X3NaliaStartFlirt","GLOBAL",1)
!StateCheck(Myself,CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
THEN
RESPONSE #100
StartDialogueNoSet(Player1)
END

IF
InParty(Myself)
RealGlobalTimerExpired("X3NaliaFlirtTimer","GLOBAL")
Global("X3DisableFlirts","LOCALS",0)
Global("X3NaliaSilence","GLOBAL",0)
OR(2)
Global("PGNaliaRomanceActive","GLOBAL",2)
Global("PGNaliaRomanceActive","GLOBAL",1)
!CombatCounter(0)
THEN
RESPONSE #100
RealSetGlobalTimer("X3NaliaFlirtTimer","GLOBAL",600)
END

//Love Talk Arnel/De'Arnise Initialization 
IF 
InParty(Myself)
Global("PGNaliaLove","GLOBAL",0)
	OR(2)
	Global("NaliaRomanceActive","GLOBAL",2)	
	Global("PGNaliaRomanceActive","GLOBAL",2)
	Global("X3NaliaLove","GLOBAL",0)
THEN RESPONSE #100 
RealSetGlobalTimer("X3NaliaToBLoveTimer","GLOBAL",2700)
SetGlobal("PGNaliaLove","GLOBAL",1)
END

//Friendship Checks (USes same timer as lovetalks.) 
IF 
	RealGlobalTimerExpired("X3NaliaToBLoveTimer","GLOBAL")
		    IsValidForPartyDialogue(Myself)
	IsValidForPartyDialogue(Player1)
    See(Player1)
    CombatCounter(0)
    !See([ENEMY])
	OR(3)
	Global("X3NaliaFriendship","GLOBAL",1)
	Global("X3NaliaFriendship","GLOBAL",3)	
	Global("X3NaliaFriendship","GLOBAL",5)
THEN RESPONSE #100 
	IncrementGlobal("X3NaliaFriendship","GLOBAL",1)
END 

//LoveTalk Checks
IF 
	RealGlobalTimerExpired("X3NaliaToBLoveTimer","GLOBAL")
	OR(2)
	Global("NaliaRomanceActive","GLOBAL",2)	
	Global("PGNaliaRomanceActive","GLOBAL",2)		
    IsValidForPartyDialogue(Myself)
	IsValidForPartyDialogue(Player1)
    See(Player1)
    CombatCounter(0)
    !See([ENEMY])
	OR(5)
	Global("PGNaliaLove","GLOBAL",1)
	Global("PGNaliaLove","GLOBAL",3)	
	Global("PGNaliaLove","GLOBAL",7)
	Global("PGNaliaLove","GLOBAL",9)
	Global("PGNaliaLove","GLOBAL",13)
THEN RESPONSE #100 
	IncrementGlobal("PGNaliaLove","GLOBAL",1)
END 

//Friendship Talk Firings 
IF 
	RealGlobalTimerExpired("X3NaliaToBLoveTimer","GLOBAL")		
    IsValidForPartyDialogue(Myself)
	IsValidForPartyDialogue(Player1)
    See(Player1)
    CombatCounter(0)
    !See([ENEMY])
	OR(3)
	Global("X3NaliaFriendship","GLOBAL",2)
	Global("X3NaliaFriendship","GLOBAL",4)	
	Global("X3NaliaFriendship","GLOBAL",6)	
THEN RESPONSE #100 
	RealSetGlobalTimer("X3NaliaToBLoveTimer","GLOBAL",1500)
	StartDialogueNoSet(Player1)	
END 

//Love Talk Firings
IF 
	RealGlobalTimerExpired("X3NaliaToBLoveTimer","GLOBAL")		
    IsValidForPartyDialogue(Myself)
	IsValidForPartyDialogue(Player1)
    See(Player1)
    CombatCounter(0)
    !See([ENEMY])
	OR(9)
	Global("PGNaliaLove","GLOBAL",2)
	Global("PGNaliaLove","GLOBAL",4)	
	Global("PGNaliaLove","GLOBAL",6)
	Global("PGNaliaLove","GLOBAL",8)	
	Global("PGNaliaLove","GLOBAL",10)
	Global("X3NaliaLove","GLOBAL",2)
	Global("X3NaliaLove","GLOBAL",4)
	Global("X3NaliaLove","GLOBAL",8)
	Global("X3NaliaLove","GLOBAL",10)	
THEN RESPONSE #100 
	//Play(0)
	//PlaySound()	
	RealSetGlobalTimer("X3NaliaToBLoveTimer","GLOBAL",2700)
	StartDialogueNoSet(Player1)	
END 

//Love Talk 5 and 1 StandAlone 
IF 
	RealGlobalTimerExpired("X3NaliaToBLoveTimer","GLOBAL")
	Global("PGNaliaRomanceActive","GLOBAL",1)
    IsValidForPartyDialogue(Myself)
	IsValidForPartyDialogue(Player1)
    See(Player1)
    CombatCounter(0)
    !See([ENEMY])
	OR(2)
	Global("PGNaliaLove","GLOBAL",1)
	Global("PGNaliaLove","GLOBAL",9)	
THEN RESPONSE #100 
	IncrementGlobal("PGNaliaLove","GLOBAL",1)	
END 

//Love Talk 2 StandAlone 
IF 
	RealGlobalTimerExpired("X3NaliaToBLoveTimer","GLOBAL")
	GlobalGT("WraithPunish","GLOBAL",1)
	Global("PGNaliaRomanceActive","GLOBAL",1)
    IsValidForPartyDialogue(Myself)
	IsValidForPartyDialogue(Player1)
    See(Player1)
    CombatCounter(0)
    !See([ENEMY])
	Global("PGNaliaLove","GLOBAL",11)
THEN RESPONSE #100 
	IncrementGlobal("PGNaliaLove","GLOBAL",1)	
END 

//Love Talk 4 StandAlone 
IF 
	RealGlobalTimerExpired("X3NaliaToBLoveTimer","GLOBAL")
	GlobalGT("TalkedToSolar","GLOBAL",2)
	Global("PGNaliaRomanceActive","GLOBAL",1)
    IsValidForPartyDialogue(Myself)
	IsValidForPartyDialogue(Player1)
    See(Player1)
    CombatCounter(0)
    !See([ENEMY])
	Global("PGNaliaLove","GLOBAL",11)
THEN RESPONSE #100 
	IncrementGlobal("PGNaliaLove","GLOBAL",1)	
END 

//Love Talk 6 StandAlone 
IF 
	RealGlobalTimerExpired("X3NaliaToBLoveTimer","GLOBAL")
	GlobalGT("TalkedToSolar","GLOBAL",3)
	Global("PGNaliaRomanceActive","GLOBAL",1)
    IsValidForPartyDialogue(Myself)
	IsValidForPartyDialogue(Player1)
    See(Player1)
    CombatCounter(0)
    !See([ENEMY])
	Global("PGNaliaLove","GLOBAL",11)
THEN RESPONSE #100 
	IncrementGlobal("PGNaliaLove","GLOBAL",1)	
END 

//Love Talk 8 De'Arnise/Arnel
IF 
	PartyRested()
	OR(2)
	Global("NaliaRomanceActive","GLOBAL",2)	
	Global("PGNaliaRomanceActive","GLOBAL",2)
    IsValidForPartyDialogue(Myself)
	IsValidForPartyDialogue(Player1)
    See(Player1)
    CombatCounter(0)
    !See([ENEMY])
	Global("PGNaliaLove","GLOBAL",15)
THEN RESPONSE #100 
	IncrementGlobal("PGNaliaLove","GLOBAL",1)	
END 

//Love Talk 8 Standalone
IF 
	PartyRested()	
	Global("PGNaliaRomanceActive","GLOBAL",2)
    IsValidForPartyDialogue(Myself)
	IsValidForPartyDialogue(Player1)
    See(Player1)
    CombatCounter(0)
    !See([ENEMY])
	Global("X3NaliaLove","GLOBAL",15)
THEN RESPONSE #100 
	IncrementGlobal("X3NaliaLove","GLOBAL",1)
END 

//Final Love Talk De'Arnise/Arnel
IF 
	GlobalGT("TalkedToSolar","GLOBAL",5)
	OR(2)
	Global("NaliaRomanceActive","GLOBAL",2)	
	Global("PGNaliaRomanceActive","GLOBAL",2)	
    IsValidForPartyDialogue(Myself)
	IsValidForPartyDialogue(Player1)
    See(Player1)
    CombatCounter(0)
    !See([ENEMY])
	Global("PGNaliaLove","GLOBAL",17)
THEN RESPONSE #100 
	IncrementGlobal("PGNaliaLove","GLOBAL",1)
END 

//Final Love Talk Standalone 
IF 
	GlobalGT("TalkedToSolar","GLOBAL",5)
	Global("PGNaliaRomanceActive","GLOBAL",2)	
    IsValidForPartyDialogue(Myself)
	IsValidForPartyDialogue(Player1)
    See(Player1)
    CombatCounter(0)
    !See([ENEMY])
	Global("X3NaliaLove","GLOBAL",17)
THEN RESPONSE #100 
	IncrementGlobal("X3NaliaLove","GLOBAL",1)
END

//Final Friendship Talk
IF 
	GlobalGT("TalkedToSolar","GLOBAL",5)
	Global("PGNaliaRomanceActive","GLOBAL",2)	
    IsValidForPartyDialogue(Myself)
	IsValidForPartyDialogue(Player1)
    See(Player1)
    CombatCounter(0)
    !See([ENEMY])
	Global("X3NaliaFriendship","GLOBAL",7)
THEN RESPONSE #100 
	IncrementGlobal("X3NaliaFriendship","GLOBAL",1)
END

IF 
    IsValidForPartyDialogue(Myself)
	IsValidForPartyDialogue(Player1)
    See(Player1)
    CombatCounter(0)
    !See([ENEMY])
	Global("X3NaliaFriendship","GLOBAL",8)
THEN RESPONSE #100 
	StartDialogueNoSet(Player1)
END 

//Fire Dialogue without setting timer
IF 
    IsValidForPartyDialogue(Myself)
	IsValidForPartyDialogue(Player1)
    See(Player1)
    CombatCounter(0)
    !See([ENEMY])
	OR(6)
	Global("PGNaliaLove","GLOBAL",14)	
	Global("PGNaliaLove","GLOBAL",16)	
	Global("PGNaliaLove","GLOBAL",18)
	Global("X3NaliaLove","GLOBAL",12)
	Global("X3NaliaLove","GLOBAL",16)
	Global("X3NaliaLove","GLOBAL",18)
THEN RESPONSE #100 
	//PlaySong(0)
	//PlaySound()
	StartDialogueNoSet(Player1)
END 
